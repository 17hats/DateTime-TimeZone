#!/usr/bin/perl -w
#
# Copyright (c) 2003-2005 Dave Rolsky.  All rights reserved.
# Copyright (c)      2005 Daisuke Maki. All rights reserved.
#

use strict;

use lib './lib';

use Data::Dumper;
use DateTime::TimeZone::OlsonDB;
use File::Copy;
use File::Find;
use File::Path;
use File::Spec;
use Getopt::Long;

$Data::Dumper::Indent = 1;
$Data::Dumper::Sortkeys = 1;
$Data::Dumper::Terse = 1;

my $VERSION = "0.08";

my $INFINITY  = 100 ** 100 ** 100;

my %opts;
GetOptions( 'dir:s'     => \$opts{dir},
            'old'       => \$opts{old},
            'file:s'    => \$opts{file},
            'name:s'    => \$opts{name},
            'version:s' => \$opts{version},
            'help'      => \$opts{help},
          );

$opts{help} = 1
    unless defined $opts{dir} && -d $opts{dir};

$opts{help} = 1
    unless defined $opts{version} || $opts{file} || $opts{name};

if ( $opts{help} )
{
    print <<'EOF';

This script parses the Olson time zone database files and turns them
into a set of Perl modules.  It also generates the MANIFEST and the
DateTime::TimeZoneCatalog module, which contains a list of all the
available time zone names.

By default, it looks for files named africa, antarctica, asia,
australasia, europe, northamerica, pacificnew, southamerica, and
backward.  All other files are ignored.

It takes the following arguments:

  --dir      A directory containing Olson db files.

  --version  The version of the Olson data files being used.
             Required unless one of the debugging options is given.

  --file     Parse just the file with the given name.  For debugging.

  --name     Only create the specified time zone.  For debugging.

  --old      Also look for files named etcetera, factory, and systemv

  --help     What you are reading

If the --file or --name options are specified, the MANIFEST and
DateTime::TimeZoneCatalog files will not be generated.

EOF

    exit;
}

my @files;

if ( $opts{file} )
{
    @files = $opts{file};
}
else
{
    @files = qw( africa antarctica asia australasia
                 europe northamerica pacificnew
                 southamerica backward
               );

    push @files, qw( etcetera factory systemv )
        if $opts{old};
}

unless ( $opts{name} || $opts{file} )
{
    copy( 'MANIFEST.base', 'MANIFEST' );
    open MAN, ">>MANIFEST" or die "Cannot write to MANIFEST: $!";
}

my ( @zones, %categories, %links );

foreach my $f ( sort @files )
{
    my $file = File::Spec->catfile( $opts{dir}, $f );

    die "No such file $file\n" unless -e $file;

    print "Now parsing $file\n";

    my $odb = DateTime::TimeZone::OlsonDB->new;

    $odb->parse_file($file);

    %links = ( %links, $odb->links );

    foreach my $zone_name ( sort $odb->zone_names )
    {
        if ( $opts{name} )
        {
            next unless $zone_name eq $opts{name};
        }
        print "  creating zone $zone_name\n";

        push @zones, $zone_name;

        my ($category, $name) = split /\//, $zone_name, 2;
        push @{ $categories{$category} }, $name;

        (my $dir = $category) =~ tr/-/_/;
        (my $outfile1 = $name) =~ tr/-/_/;

        (my $mod_name = $zone_name) =~ s/\//::/g;
        $mod_name =~ tr/-/_/;

        my $max_year = (localtime)[5] + 1910;
        my $zone = $odb->expanded_zone( name => $zone_name,
                                        expand_to_year => $max_year,
                                      );

        my $from = "Generated from $file.";
        $from .= "  Olson data version $opts{version}"
            if defined $opts{version};

        my $pm_body = generate_pm($mod_name, $zone, $max_year, $from);
        my $pp_body = generate_pp($mod_name, $zone, $max_year, $from);
        my $xs_body = generate_xs($mod_name, $zone, $max_year, $from, $zone_name);

        my @name_pieces   = split /\//, $outfile1;
        my $base_filename = pop @name_pieces;
        my $pm_filename   = $base_filename . '.pm';
        my $pp_filename   = $base_filename . 'PP.pm';
        my $xs_filename   = $base_filename . '.xs';

        my $outdir = File::Spec->catdir( qw( lib DateTime TimeZone ),
                                         $dir, @name_pieces  );

        mkpath( $outdir, 1, 0755 );

        # Write to .pm
        my $pm_outfile = File::Spec->catfile( $outdir, $pm_filename );
        open OUT,">$pm_outfile" or die "Cannot write to $pm_outfile: $!";
        print OUT $pm_body or die "Cannot write to $pm_outfile: $!";
        close (OUT) or die "Cannot write to $pm_outfile: $!";

        # Write pure-perl .pm
        my $pp_outfile = File::Spec->catfile( $outdir, $pp_filename );
        open OUT,">$pp_outfile" or die "Cannot write to $pp_outfile: $!";
        print OUT $pp_body or die "Cannot write to $pp_outfile: $!";
        close (OUT) or die "Cannot write to $pp_outfile: $!";

        # Write to .xs
        my $xs_outfile = File::Spec->catfile( $outdir, $xs_filename );
        open OUT,">$xs_outfile" or die "Cannot write to $xs_outfile: $!";
        print OUT $xs_body or die "Cannot write to $xs_outfile: $!";
        close (OUT) or die "Cannot write to $xs_outfile: $!";

        unless ( $opts{name} || $opts{file} )
        {
            print MAN "$pm_outfile\n" or die "Cannot write to MANIFEST: $!";
            print MAN "$xs_outfile\n" or die "Cannot write to MANIFEST: $!";
            print MAN "$pp_outfile\n" or die "Cannot write to MANIFEST: $!";
        }
    }
}

exit if $opts{name};

# override some links and add others
%links =
    ( %links,
      'Etc/GMT+0'   => 'UTC',
      'Etc/Universal' => 'UTC',
      'Etc/Zulu'    => 'UTC',
      'Etc/UCT'     => 'UTC',
      'GMT0'        => 'UTC',
      'GMT'         => 'UTC',
    );

delete $links{UTC};

# This links to America/Indiana in the "backward" file
delete $links{EST};
# And this one is for America/Phoenix
delete $links{MST};

my $links = Dumper \%links;
$links =~ s/{/(/;
$links =~ s/}/)/;

my $zones = join "\n", map { "  $_" } sort @zones;
my $cat_names = join "\n", map { "  $_" } sort keys %categories;
my $cat = '';
foreach my $c ( sort keys %categories )
{
    $cat .= "'$c' => [ qw(\n";
    $cat .= join "\n", map { "  $_" } sort @{ $categories{$c} };
    $cat .= "\n) ],\n";
}
my $zonecatalog = <<"EOF";
# This file is auto-generated by the Perl DateTime Suite VTIMEZONE
# parser ($VERSION).
#
# Do not edit this file directly.

package DateTime::TimeZone;

use strict;

\@DateTime::TimeZone::ALL =
qw(
$zones
);

\@DateTime::TimeZone::CATEGORY_NAMES =
qw(
$cat_names
);

\%DateTime::TimeZone::CATEGORIES =
(
$cat
);

\%DateTime::TimeZone::LINKS =
$links
;

sub all_names { wantarray ? \@DateTime::TimeZone::ALL : \\\@DateTime::TimeZone::ALL }
sub categories { wantarray ? \@DateTime::TimeZone::CATEGORY_NAMES : \\\@DateTime::TimeZone::CATEGORY_NAMES }
sub links { wantarray ? %DateTime::TimeZone::LINKS : \\%DateTime::TimeZone::LINKS }

sub names_in_category
{
    return unless exists \$DateTime::TimeZone::CATEGORIES{ \$_[0] };

    return wantarray ? \@{ \$DateTime::TimeZone::CATEGORIES{ \$_[0] } } : \$DateTime::TimeZone::CATEGORIES{ \$_[0] };
}

1;

__END__

=head1 NAME

DateTime::TimeZoneCatalog - Provides a list of all valid time zone names

=head1 SYNOPSIS

See DateTime::TimeZone for usage details.

=head1 DESCRIPTION

This module contains an enumerated list of all known system timezones,
so that applications can easily present a list of timezones.

=cut

EOF

open OUT2, ">lib/DateTime/TimeZoneCatalog.pm" or die $!;
print OUT2 $zonecatalog or die $!;
close OUT2 or die $!;

sub generate_pm
{
    my($mod_name, $zone, $max_year, $from) = @_;
    my $spans = serialize_spans(zone_as_spans($zone));

    $spans =~ s/-inf/&DateTime::TimeZone::NEG_INFINITY/g;
    $spans =~ s/inf/&DateTime::TimeZone::INFINITY/g;

    $spans =~ s/('(?:start|end)_date'\s+=>\s+)'(\d+)'/$1$2/g;

    my $generator = pm_zone_generator($zone);

    return <<"EOF";
# This file is auto-generated by the Perl DateTime Suite time zone
# code generator ($VERSION) This code generator comes with the
# DateTime::TimeZone module distribution in the tools/ directory
#
# $from
#
# Do not edit this file directly.
#
package DateTime::TimeZone::$mod_name;

use strict;

use Class::Singleton;
use DateTime::TimeZone;
use DateTime::TimeZone::OlsonDB;

\@DateTime::TimeZone::${mod_name}::ISA = ( 'Class::Singleton', 'DateTime::TimeZone' );

my \$loaded_xs;
BEGIN
{
    \$loaded_xs = 0;
    unless ( \$ENV{PERL_DATETIME_TIMEZONE_PP} )
    {
        eval
        {
            if ( \$] >= 5.006 )
            {
                require XSLoader;
                XSLoader::load('DateTime::TimeZone::$mod_name');
            }
            else
            {
                require DynaLoader;
                push \@DateTime::TimeZone::${mod_name}::ISA, 'DynaLoader';
                DateTime::TimeZone::${mod_name}->bootstrap('DateTime::TimeZone::$mod_name');
            }

            \$loaded_xs = 1;
        };
        die if \$@ && \$@ !~ /object version/;
    }

    unless (\$loaded_xs)
    {
        require DateTime::TimeZone::${mod_name}PP;
    }
}

BEGIN { __PACKAGE__->bootstrap_spans() if \$loaded_xs }
END { __PACKAGE__->free_spans() if \$loaded_xs }

sub _new_instance
{
    return shift->_init( \@_ );
}

$generator

1;

EOF
}

sub generate_pp
{
    my($mod_name, $zone, $max_year, $from) = @_;

    my $pp_generator = pp_zone_generator($zone);
    my $spans = serialize_spans(zone_as_spans($zone));

    $spans =~ s/-inf/DateTime::TimeZone::NEG_INFINITY/g;
    $spans =~ s/inf/DateTime::TimeZone::INFINITY/g;

    $spans =~ s/('(?:start|end)_date'\s+=>\s+)'(\d+)'/$1$2/g;

    return <<"EOF";
# This file is auto-generated by the Perl DateTime Suite time zone
# code generator ($VERSION) This code generator comes with the
# DateTime::TimeZone module distribution in the tools/ directory
#
# $from
#
# Do not edit this file directly.
#
package DateTime::TimeZone::$mod_name;

use strict;

use Class::Singleton;
use DateTime::TimeZone;
use DateTime::TimeZonePP;
use DateTime::TimeZone::OlsonDB;

\@DateTime::TimeZone::${mod_name}::ISA = ( 'Class::Singleton', 'DateTime::TimeZone' );

my \$spans =
$spans;

sub _spans { \$spans }
sub max_span { \$spans->[-1] }
sub _max_year { $max_year }
sub push_span { shift; push @\$spans, shift }

$pp_generator

1;

EOF
}


sub generate_xs
{
    my($mod_name, $zone, $max_year, $from, $zone_name) = @_;

    (my $xs_name_space = lc $zone_name) =~ s/[\/-]/_/g;
    $xs_name_space =~ s/^/__/;

    my $xs_generator = xs_zone_generator($zone);
    my $xs_spans_bootstrap =
        xs_serialize_spans($xs_name_space, zone_as_spans($zone));
    $xs_spans_bootstrap =~ s/-inf\.0/NEG_INFINITY/g;
    $xs_spans_bootstrap =~ s/inf\.0/INFINITY/g;
    my $xs_body = <<"EOF";
/********************************************************************
 *
 * DateTime::TimeZone::${mod_name}
 *
 * This file is auto-generated by the Perl Datetime suite time zone
 * code generator ($VERSION) This code generateor comes with the 
 * DateTime::TimeZone module distribution in the tools/ directory
 *
 * $from
 *
 * Do no edit this file directly.
 *
 ********************************************************************/

#include "EXTERN.h"
#include "perl.h"
#include "XSUB.h"
#include "ppport.h"
#include "timezone.h"
#include "util.h"

#ifndef INFINITY
#define INFINITY 1e50f
#endif

#ifndef NEG_INFINITY
#define NEG_INFINITY -1 * INFINITY
#endif

static dtz_span **${xs_name_space}_spans = NULL;
static int      spanset_size  = 0;
static int      spanset_count = 0;

static void
push_span(dtz_span **list, dtz_span *s)
{
    if (spanset_size <= spanset_count) {
        spanset_size += 8;
        Renew(${xs_name_space}_spans, spanset_size, dtz_span *);
    }

    ${xs_name_space}_spans[spanset_count++] = s;
}

static void
free_spans()
{
    Safefree(${xs_name_space}_spans);
}

MODULE = DateTime::TimeZone::$mod_name  PACKAGE = DateTime::TimeZone::$mod_name

PROTOTYPES: ENABLE

$xs_spans_bootstrap

void
free_spans(class)
        SV *class;
    CODE:
        free_spans();

SV *
push_span(class, span_data)
        SV *class;
        AV *span_data;
    PREINIT:
        dtz_span  *span;
        SV       **svp;
        STRLEN     len;
        char      *short_name;
    CODE:
        Newz(1234, span, 1, dtz_span);

        svp = av_fetch(span_data, 0, 0);
        span->utc_start   = svp ? SvNV(*svp) : 0;

        svp = av_fetch(span_data, 1, 0);
        span->utc_end     = svp ? SvNV(*svp) : 0;

        svp = av_fetch(span_data, 2, 0);
        span->local_start = svp ? SvNV(*svp) : 0;

        svp = av_fetch(span_data, 3, 0);
        span->local_end   = svp ? SvNV(*svp) : 0;

        svp = av_fetch(span_data, 4, 0);
        span->offset      = svp ? SvNV(*svp) : 0;

        svp = av_fetch(span_data, 5, 0);
        span->is_dst      = svp ? SvIV(*svp) : 0;

        svp = av_fetch(span_data, 6, 0);
        if (!svp) 
            croak("No name supplied");
        short_name = SvPV(*svp, len);
        Copy(short_name, span->short_name, len, char);
        push_span(${xs_name_space}_spans, span);

AV *
max_span(class)
        SV *class;
    PREINIT:
        AV *av;
        dtz_span *span = ${xs_name_space}_spans[spanset_count - 1];
    CODE:
        av = newAV();
        av_push(av, newSVnv(span->utc_start));
        av_push(av, newSVnv(span->utc_end));
        av_push(av, newSVnv(span->local_start));
        av_push(av, newSVnv(span->local_end));
        av_push(av, newSVnv(span->offset));
        av_push(av, newSViv(span->is_dst));
        av_push(av, newSVpv(span->short_name, strlen(span->short_name)));
        RETVAL = av;
    OUTPUT:
        RETVAL

SV *
_max_year (self)
        SV *self;
    CODE:
        RETVAL = newSViv($max_year);
    OUTPUT:
        RETVAL

SV *
_spans_binary_search(self, type, seconds)
        SV *self;
        SV *type;
        SV *seconds;
    PREINIT:
        AV *ret;
    CODE:
        ret = util_spans_binary_search(self, type, seconds, ${xs_name_space}_spans, spanset_count);
        RETVAL = ret ?
           newRV_noinc((SV *) ret) : &PL_sv_undef; 
    OUTPUT:
        RETVAL 


$xs_generator
EOF
}

sub zone_as_spans
{
    my $zone = shift;

    my @spans;

    my @changes = $zone->sorted_changes;

    for ( my $x = 1; $x < @changes; $x++ )
    {
        my $last_total_offset = $x > 1 ? $changes[ $x - 2 ]->total_offset : undef;

        my $span =
            DateTime::TimeZone::OlsonDB::Change::two_changes_as_span
                ( @changes[ $x - 1, $x ], $last_total_offset );

        push @spans, $span;

        if (@spans > 2)
        {
            die "Gap in UTC end/start datetime for " . $zone->name
                unless $spans[-2]{utc_end} == $spans[-1]{utc_start};
        }
    }

    unless ( $zone->infinite_rules )
    {
        my $last_observance = $changes[-1]->observance;

        my $utc_start =
            @spans ? $spans[-1]{utc_end} : -1 * $INFINITY;

        push @spans, { utc_start   => $utc_start,
                       utc_end     => $INFINITY,
                       local_start => $utc_start - $last_observance->total_offset,
                       local_end   => $INFINITY,
                       short_name  => sprintf( $last_observance->format, '' ),
                       offset      => $last_observance->total_offset,
                       is_dst      => 0,
                     };
    }

    return \@spans;
}

sub serialize_spans
{
    my $spans = shift;

    my $string = "[\n";
    $string .= join "\n", map { serialize_span($_) } @$spans;
    $string .= "\n]";

    return $string;
}

sub serialize_span
{
    my $span = shift;
    # must correspond to constants in DT::TZ, and short_name is always last
    my @keys = qw( utc_start utc_end local_start local_end offset is_dst );
    my $string = "    [\n";
    $string .= join ",\n", @$span{@keys};
    $string .= ",\n'$span->{short_name}'";
    $string .= "\n    ],";

    return $string;
}

sub xs_serialize_spans
{
    my $xs_name_space = shift;
    my $spans = shift;
    my $starting_size = scalar @$spans;

    my $string = <<EOF;
void
bootstrap_spans(class)
        SV *class;
    PREINIT:
        dtz_span *span;
    CODE:
        if (${xs_name_space}_spans == NULL) {
            spanset_size = $starting_size;
            Newz(1234, ${xs_name_space}_spans, spanset_size, dtz_span *);
EOF

    foreach my $span (@$spans) {
        my $short_name_len = length($span->{short_name});
        $string .= <<EOF;
            Newz(1234, span, 1, dtz_span);
            span->utc_start   = $span->{utc_start}.0;
            span->utc_end     = $span->{utc_end}.0;
            span->local_start = $span->{local_start}.0;
            span->local_end   = $span->{local_end}.0;
            span->offset      = $span->{offset};
            span->is_dst      = $span->{is_dst};
            Copy("$span->{short_name}", span->short_name, $short_name_len, char);
            push_span(${xs_name_space}_spans, span);

EOF
    }
    $string .= <<EOF;
        }
EOF

    return $string;
}

sub xs_zone_generator
{
    my $zone = shift;

    return '' unless $zone->infinite_rules;

    my $last_observance = ($zone->sorted_changes)[-1]->observance;

    # hack to trim size of dumped object
    delete $last_observance->{utc_start_datetime}{locale};
    delete $last_observance->{local_start_datetime}{locale};
    delete $last_observance->{utc_start_datetime}{local_c};
    delete $last_observance->{local_start_datetime}{local_c};
    delete $last_observance->{rules};
    delete $last_observance->{first_rule};

    # This assumes that there is only one observance from end of
    # changes til end of time, which should be guaranteed by code in
    # OlsonDB module.
    my $offset = $last_observance->total_offset;

    my $xs_generator = <<EOF;

SV *
_last_offset(self)
        SV *self;
    CODE:
        RETVAL = newSViv($offset);
    OUTPUT:
        RETVAL

EOF

    return $xs_generator ;
}

sub pp_zone_generator
{
    my $zone = shift;

    return '' unless $zone->infinite_rules;

    my $generator = <<'EOF';
sub _last_offset { !OFFSET }
EOF

    my $last_observance = ($zone->sorted_changes)[-1]->observance;

    # This assumes that there is only one observance from end of
    # changes til end of time, which should be guaranteed by code in
    # OlsonDB module.
    my $offset = $last_observance->total_offset;
    $generator =~ s/!OFFSET/$offset/g;

    return $generator;
}

sub pm_zone_generator
{
    my $zone = shift;

    return '' unless $zone->infinite_rules;

    my $generator = <<'EOF';
my $last_observance = !LAST_OBSERVANCE;
sub _last_observance { $last_observance }

my $rules = !RULES;
sub _rules { $rules }
EOF

    my $last_observance = ($zone->sorted_changes)[-1]->observance;

    # hack to trim size of dumped object
    delete $last_observance->{utc_start_datetime}{locale};
    delete $last_observance->{local_start_datetime}{locale};
    delete $last_observance->{utc_start_datetime}{local_c};
    delete $last_observance->{local_start_datetime}{local_c};
    delete $last_observance->{rules};
    delete $last_observance->{first_rule};

    # This assumes that there is only one observance from end of
    # changes til end of time, which should be guaranteed by code in
    # OlsonDB module.
    my $offset = $last_observance->total_offset;

    my @rules = $zone->infinite_rules;

    # This is cleaner than making the above a double-quoted string
    $generator =~ s/!RULES/Dumper \@rules/eg;
    $generator =~ s/!LAST_OBSERVANCE/Dumper $last_observance/eg;
    $generator =~
        s/\$VAR1->{'local_start_datetime'}{'tz'}/bless( {
      'name' => 'floating',
      'offset' => 0
    }, 'DateTime::TimeZone::Floating' )/;
    $generator =~
        s/\$VAR1->{'utc_start_datetime'}{'tz'}/bless( {
      'name' => 'floating',
      'offset' => 0
    }, 'DateTime::TimeZone::Floating' )/;
    $generator =~ s/!OFFSET/$offset/g;
    $generator =~ s/!OFFSET/$offset/g;

    return $generator;
}
